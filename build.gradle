apply plugin: 'com.android.application'
apply from: 'version.gradle'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"

    defaultConfig {
        applicationId "com.konka.gradleconfig"
        minSdkVersion 17
        targetSdkVersion 26
        versionCode appVersionCode
        versionName appVersionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField("String", "name", '"hwanj"')
        multiDexEnabled true
        resConfigs 'zh' //指定中文类型的资源才打包到apk中
    }

    flavorDimensions "dimension1", "dimension2"//定义了两个维度（分组），顺序决定优先级
    productFlavors {
        google {
            applicationId 'com.hwanj.google'
            dimension 'dimension1'
            resValue("string", "tip", "google 欢迎你")
            resValue("id", "tipId", "1122002")
        }
        baidu {
            applicationId 'com.hwanj.baidu'
            dimension 'dimension2'
            resValue("string", "tip", "baidu 欢迎你")
        }
    }

    signingConfigs {
        signRelease {
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storeFile file("F:/key/platform.keystore")
            storePassword "android"
        }
        signDebug {

        }
    }

    buildTypes {
        release {
            resValue("id", "custom_id", "112200")
//            buildConfigField("String", "name", '"hwanj"')
            manifestPlaceholders.put("action", "android.intent.action.MAIN")
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            multiDexEnabled true
            signingConfig signingConfigs.signRelease
        }
        debug {
            manifestPlaceholders.put("action", "android.intent.action.MAIN")
            debuggable true
            minifyEnabled false
            applicationIdSuffix ".debug"
        }
    }

    applicationVariants.all {
        variant ->
//            def parent = new File(project.rootDir.absolutePath + "/${project.name}/build/outputs/hwanjApk")
//            variant.getPackageApplication().outputDirectory = parent
            variant.getPackageApplication().outputScope.apkDatas.each {
                apkData ->
                    def fileName = "gradleconfig_${variant.buildType.name}_v${variant.versionName}_${buildTime()}.apk"
                    apkData.outputFileName = fileName
            }
    }

    productFlavors.all {
        productFlavor -> println productFlavor.name + 'hwanj'
    }

    compileOptions {
        setEncoding 'utf-8'
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    adbOptions {
        installOptions '-r', '-d'
    }

    dexOptions {

    }

    lintOptions {
        abortOnError true     //检查到错误时停止构建
        warningsAsErrors true
        check 'NewApi'
        ignoreWarnings false   //是否忽略警告
    }
}

def buildTime() {
    def date = new Date()
    return date.format("yyyyMMdd")
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.android.support:appcompat-v7:26.0.0-beta1'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:0.5'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:2.2.2'
    implementation 'com.android.support:multidex:1.0.2'
}
